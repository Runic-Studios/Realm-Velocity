@Library('Jenkins-Shared-Lib') _

pipeline {
    agent {
        kubernetes {
            yaml jenkinsAgent("registry.runicrealms.com")
        }
    }

    environment {
        DEPLOYMENT_REPO = 'git@github.com:Runic-Studios/Realm-Deployment.git'
        IMAGE_NAME = 'realm-velocity'
    }

    stages {
        stage('Send Discord Notification (Build Start)') {
            steps {
                discordNotifyStart('Realm Velocity', env.GIT_URL, env.GIT_BRANCH, env.GIT_COMMIT.take(7))
            }
        }
        stage('Determine Environment') {
            steps {
                script {
                    def branchName = env.GIT_BRANCH.replaceAll(/^origin\//, '').replaceAll(/^refs\/heads\//, '')

                    echo "Using normalized branch name: ${branchName}"

                    if (branchName == 'dev') {
                        env.RUN_MAIN_DEPLOY = 'false'
                        env.ARTIFACT_TAG = 'latest-dev'
                    } else if (branchName == 'main') {
                        env.RUN_MAIN_DEPLOY = 'true'
                        env.ARTIFACT_TAG = 'latest'
                    } else {
                        error "Unsupported branch: ${branchName}"
                    }
                }
            }
        }
        stage('Pull Plugin Artifacts') {
            steps {
                container('jenkins-agent') {
                    script {
                        def manifest = readYaml file: 'plugin-manifest.yaml'
                        manifest.artifacts.each { artifact ->
                            def parts = artifact.name.tokenize('/')
                            def registry = parts[0]
                            def namespace = parts[1]
                            def repo = parts[2]
                            def artifactName = parts[3]

                            echo "Pulling ${artifactName} from ${registry}/${namespace}/${repo} with tag ${artifact.newTag}"
                            orasPull(artifactName, artifact.newTag, repo, registry, namespace)
                        }
                    }
                }
            }
        }
        stage('Build and Push Docker Image') {
            steps {
                container('jenkins-agent') {
                    dockerBuildPush(IMAGE_NAME, env.GIT_COMMIT.take(7), "registry.runicrealms.com", "build")
                }
            }
        }
        stage('Update Deployment') {
            steps {
                container('jenkins-agent') {
                    updateManifest('dev', 'Realm-Deployment', 'base/kustomization.yaml', IMAGE_NAME, env.GIT_COMMIT.take(7), "registry.runicrealms.com", "build")
                }
            }
        }
        stage('Create PR to Promote Dev to Main (Prod Only)') {
            when {
                expression { return env.RUN_MAIN_DEPLOY == 'true' }
            }
            steps {
                script {
                    withCredentials([sshUserPrivateKey(credentialsId: 'github-ssh', keyFileVariable: 'SSH_KEY')]) {
                        sh """
                          export GIT_SSH_COMMAND='ssh -i $SSH_KEY -o StrictHostKeyChecking=no'
                          gh auth setup-git
                          gh pr create --base main --head dev --title 'AUTOGENERATED: Promote latest Realm-Velocity image to production' \
                            --body 'This PR promotes the latest tested Realm-Velocity build from dev to production. This was triggered because of a push to Realm-Velocity on ref main.'
                        """
                    }
                }
            }
        }
    }
    post {
        success {
            discordNotifySuccess('Realm Velocity', env.GIT_URL, env.GIT_BRANCH, env.GIT_COMMIT.take(7))
        }
        failure {
            discordNotifyFail('Realm Velocity', env.GIT_URL, env.GIT_BRANCH, env.GIT_COMMIT.take(7))
        }
    }
}
